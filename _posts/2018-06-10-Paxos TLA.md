---
title:  “Paxos TLA+”
date:   2018-06-10 08:00:12 +0800
categories: distributed system
---

# Paxos
Paxos集群不断重复地在运行Paxos协议，我们可以称之为回合，每一个Paxos回合都有若干轮，每一轮都包括两个阶段。
回合的发起者是Proposer，Proposer会发起一个回合当且仅当其受到Client的请求同时可以连接到多数Acceptor(Quorum)。


## 阶段 1a：Prepare
在这一阶段Proposer创建一个编号为N的提议，这里N必须大于该Proposer之前使用所有提议编号。
然后Proposer将这条提议发送到Quorum(Proposer可以任意选择一个Quorum来发送消息)。
```
(***************************************************************************)
(* TLA模拟中，并没有保证b大于Proposer之前使用的最大提议编号，而是遍历Ballot编号。    *)
(* 此外，消息msgs是一个全局变量，即消息{type: "1a", bal: b}被发送给了所有Acceptor  *)
(***************************************************************************)
Phase1a(b) == /\ Send([type |-> "1a", bal |-> b])
              /\ UNCHANGED <<maxBallot, acceptedBallot, acceptedValue>>
```

## 阶段 1b：Promise
当Acceptor接受到来自阶段1a的Prepare消息，如果Proposer发送的提议编号大于Acceptor之前收到的所有提议的编号，
那么Acceptor必须返回对应的Promise消息，Promise消息并不表示提议被同意，而是表明从现在开始Acceptor会忽略所有提议编号小于N的提议。
不过，如果Acceptor在收到这则消息之前已经同意(Accpetd)了另一个提议，那么他会在返回的Promise消息中附加对应的提议值。

如果新的提议编号小于Acceptor曾经发送Promise时对应的提议编号，那么Acceptor可以直接忽略这则消息。
不过Acceptor最好可以回复消息，明确拒绝当前提议，让Proposer可以早一点开始新一轮的提议。
```
(***************************************************************************)
(* 前两项保证Acceptor忽略提议编号小于N的提议                                    *)
(* 后两项更新Acceptor收到的最大提议编号，并将已经同意的提议编号和提议值回复给Proposer *)
(***************************************************************************)
Phase1b(a) == /\ \E m \in msgs :
                  /\ m.type = "1a"
                  /\ m.bal > maxBallot[a]
                  /\ maxBallot' = [maxBallot EXCEPT ![a] = m.bal]
                  /\ Send([type |-> "1b", acc |-> a, bal |-> m.bal,
                            mbal |-> acceptedBallot[a], mval |-> acceptedValue[a]])
              /\ UNCHANGED <<acceptedBallot, acceptedValue>>
```

## 阶段 2a：Accept
如果Proposer收到足够的Promise消息(来自于Quorum，也就是多数Acceptor)，那么它就可以为之前的提议设置对应的提议值了。
如果Proposer发现收到的Promise消息中包含了提议值，那么它必须要把本次提议的提议值设置为Promise消息中的提议值。
如果Promise消息中没有附加提议值，那么Proposer可以随意指定提议值。

在这个阶段，Proposer给Acceptor发送包含提议值的Accept消息。
```
(***************************************************************************)
(* 首先保证没有Proposer发出过编号N的Accept提议                                  *)
(* Q1b定义了来自Quorum的Promise消息集合，即多数Acceptor接受了Prepare消息；Q1bv定义 *)
(* 了返回Promise消息中之前已经同意了另一个提议的                                 *)
(* 要么Q1bv为空，可以随意指定提议值；要么选择已经同意提议中编号最大的提议，将其提议值   *)
(* 设置为本次提议值                                                           *)
(***************************************************************************)
Phase2a(b, v) == /\ ~ \E m \in msgs : m.type = "2a" /\ m.bal = b
                 /\ \E Q \in Quorum :
                    LET Q1b == {m \in msgs : /\ m.type = "1b"
                                 /\ m.acc \in Q
                                 /\ m.bal = b}
                        Q1bv == {m \in Q1b : m.mbal \geq 0}
                    IN  /\ \A a \in Q : \E m \in Q1b : m.acc = a
                        /\ \/ Q1bv = {}
                           \/ \E m \in Q1bv :
                                /\ m.mval = v
                                /\ \A mm \in Q1bv : m.mbal \geq mm.mbal
                 /\ Send([type |-> "2a", bal |-> b, val |-> v])
                 /\ UNCHANGED <<maxBallot, acceptedBallot, acceptedValue>>
```

## 阶段 2b：Accepted
当Acceptor收到提议编号为N的Accept消息，Acceptor会同意这个提议当且仅当Acceptor没有给其他Proposer发送编号大于N
的Promise消息。换句话说，如果在这个期间，Acceptor收到了编号为N+1的提议并且发出了Promise消息，那么它就不能同意当前
的Acceptor消息，因为当前消息的提议编号为N，小于N+1。

如果Acceptor同意了当前的Accept消息，那么他会记录下当前的提议值，然后通知每一个Learner当前的结果。
```
(***************************************************************************)
(* 保证Acceptor忽略提议编号小于N的提议                                         *)
(* 更新最大提议编号，已同意的提议编号和已同意提议值，并回复Accepted消息              *)
(***************************************************************************)
Phase2b(a) == \E m \in msgs : /\ m.type = "2a"
                              /\ m.bal \geq maxBallot[a]
                              /\ maxBallot' = [maxBallot EXCEPT ![a] = m.bal]
                              /\ acceptedBallot' = [acceptedBallot EXCEPT ![a] = m.bal]
                              /\ acceptedValue' = [acceptedValue EXCEPT ![a] = m.val]
                              /\ Send([type |-> "2b", acc |-> a, bal |-> m.bal, val |-> m.val])
```

## Init & Next & Spec
```
(***************************************************************************)
(* Below are defined the next-state action and the complete spec.          *)
(***************************************************************************)
Next == \/ \E b \in Ballot : \/ Phase1a(b)
                             \/ \E v \in Value : Phase2a(b, v)
        \/ \E a \in Acceptor : Phase1b(a) \/ Phase2b(a)

Spec == Init /\ [][Next]_<<maxBallot, acceptedBallot, acceptedValue, msgs>>
```

## 提示
- 如果有多个Proposer同时发送Prepared消息，当前轮很有可能会失败。如果Proposer没有收到足够多的来自Acceptor的回复，
  那么它应该认定当前轮是失败的，这种情况下，Proposer应该开启新一轮Paxos流程(注意是新一轮，不是新一回合，在一回合包含多轮)。
- 在这个阶段，Acceptor是有可能同时同意多个提议的。不过由于Quorum概念的引入，可以保证最终只会有一个提议值被Learner学习。
  一台机器会忽略来自Acceptor的单条消息，除非这则消息同时来自于Quorum。Learner不会学习提议值，除非有超过半数的Acceptor通知了这个提议值。
- 注意到当Acceptor同意一个提议它们也会同时通知Proposer，Paxos也可以用来选举Leader。

## Toolbox测试运行
配置`Acceptor = {"a1", "a2", "a3"}`, `Value = {"v1", "v2"}`, `Quorum = { {"a1", "a2"}, {"a1", "a3"}, {"a2", "a3"} }`,
`Ballot == 1..3`  
![image01]({{site.baseurl}}/image/tla_paxos_conf.png)

约180,000+个状态，没有发现错误。  
![image02]({{site.baseurl}}/image/tla_paxos_result.png)
